= Twitch
:images: /img

== Twitch network analysis

Twitch is an online platform that allows users to share their content via live stream.
Twitch streamers broadcast their gameplay or activity by sharing their screen with fans who can hear and watch them live.

This guide will show you how to:

* Perform exploratory graph analysis
* Analyze the network of users
* Find communities of streamers with shared audience

== Graph model

image::{images}/graph-model.png[]

The Twitch social network composes of users.
A small percent of those users broadcast their gameplay or activities through live streams.
In the graph model, users who do live streams are tagged with a secondary label Stream.
Additional information about which teams they belong to, which games they play on stream, and in which language they present their content is present.
You also know how many followers they had at the moment of scraping, the all-time historical view count, and when they created their user account.
The most relevant information for network analysis is knowing which users engaged in the streamerâ€™s chat.
You can distinguish if the user who chatted in the stream was a regular user (CHATTER relationship), a moderator of the stream (MODERATOR relationship), or a VIP of the stream.

_The network information was scraped between the 7th and the 10th of May 2021._


== Graph statistics

To learn how many nodes and of which labels there are in the network, you can use the _apoc.meta.stats_ procedure.

[source,cypher]
----
CALL apoc.meta.stats()
YIELD labels
----

You can observe that almost all of the nodes in our graph are users.
There are 10.5 million users and only around 6000 of them are streamers.
These streamers have played 594 games and broadcasted in 29 different languages.

== Exploring streamer's view and follower count

The total view count and the number of followers for each streamer are stored as node properties.
You can find the most viewed and most followed streamers using the `ORDER BY` and `LIMIT` clauses.

Find streamers with the most all-time view count.

[source,cypher]
----
MATCH (u:Stream)
WHERE exists(u.total_view_count)
RETURN u.name as streamer,
       u.total_view_count as total_view_count
ORDER BY total_view_count DESC 
LIMIT 10;
----

Find streamers with the highest count of followers.

[source,cypher]
----
MATCH (u:Stream)
WHERE exists(u.followers)
RETURN u.name as streamer,
       u.followers as followers
ORDER BY followers DESC 
LIMIT 10;
----

_Only streamers that have been active on the weekend between 7th and the 10th of May 2021 are included._

== Cypher implicit aggregations

When using aggregation operators in Cypher, you must remember that Cypher uses implicit aggregation.
All the non-aggregated variables that are in the `RETURN` or the `WITH` clause, will be used as grouping keys.
In this example, you will count the number of streamers per their account creation year.

[source,cypher]
----
MATCH (u:Stream)
WHERE exists(u.createdAt)
RETURN u.createdAt.year as year, 
       count(*) as countOfNewStreamers
ORDER BY year;
----

== Counting the number of relationships per node

Neo4j maintains a transactional count store for holding relationship count store for each node.
To access the relationship count store, you can use the `size()` clause.
Obtaining counts from the count store is constant-time, which can significantly optimize the performance of Cypher queries.
Here, you will inspect the games that have the highest count of streamers playing them.

[source,cypher]
----
MATCH (g:Game)
RETURN g.name as game,
       size((g)<-[:PLAYS]-()) as number_of_streamers
ORDER BY number_of_streamers DESC
LIMIT 10
----

Find users with the highest count of VIP relationships.

[source,cypher]
----
MATCH (u:User)
RETURN u.name as user, 
       size((u)-[:VIP]->()) as number_of_vips
ORDER BY number_of_vips DESC LIMIT 10;
----

Find users with the highest count of MODERATOR relationships.

[source,cypher]
----
MATCH (u:User)
RETURN u.name as user, 
       size((u)-[:MODERATOR]->()) as number_of_mods
ORDER BY number_of_mods DESC LIMIT 10;
----

== Cypher subqueries

`CALL` clause allows to execute subqueries, i.e. queries inside of other queries.
Subqueries allow you to compose queries, which is especially useful when working with aggregations or limiting results.
In this example, you will first match a single streamer.
Next, you will use a subquery to match five streamers and separately five users that chatted in the original streamer's broadcast.

[source,cypher]
----
MATCH (s:Stream)
WITH s LIMIT 1
CALL {
    WITH s
    MATCH p=(s)<--(:Stream)
    RETURN p
    LIMIT 5
    UNION
    WITH s
    MATCH p=(s)<--(:User)
    RETURN p
    LIMIT 5
}
RETURN p
----

You can observe that streamers behave like regular users.
They can chat in other streamer's broadcasts, be their moderator or VIP.

== Node degree distribution

Node degree is simply the count of relationships each node has.
Here, we are dealing with a directed network as the relationship direction holds semantic value.
When dealing with directed networks, you can split the node degree distribution into in-degree, where you count incoming relationships, and out-degree, where you are counting outgoing connections. 
First, you will examine the out-degree distribution of the user chatter network.
Using the `apoc.agg.statistics` procedure you can evaluate the distribution by looking at various percentile values of the values.

[source,cypher]
----
MATCH (u:User)
WITH u, size((u)-[:CHATTER|VIP|MODERATOR]->()) as node_outdegree
RETURN apoc.agg.statistics(node_outdegree) as statistics
----

Next, you will inspect the in-degree distribution of the user chatter network.
Remember, only streamers have incoming relationships, so we can skip inspecting regular users as we already know their in-degree is zero.

[source,cypher]
----
MATCH (u:Stream)
WITH u, size((u)<-[:CHATTER|VIP|MODERATOR]-()) as node_indegree
RETURN apoc.agg.statistics(node_indegree) as statistics
----

== Graph Data Science library

The Neo4j Graph Data Science library (GDS) features more than 50 graph algorithms, ranging from centrality to community detection and node embedding algorithms.
The GDS library executes graph algorithms on a specialized in-memory graph format to improve the performance and scale of graph algorithms.
Using native or cypher projections, we can project the stored graph in our database to the in-memory graph format.
You will begin by projecting all `User` and `Stream` nodes and the possible relationships between them, which are `CHATTER`, `MODERATOR`, and `VIP`.

[source,cypher]
----
CALL gds.graph.create('twitch', 
  ['User', 'Stream'], 
  ['CHATTER', 'VIP', 'MODERATOR'])
----

== Weakly Connected Components

The Weakly connected components algorithm (WCC) is used to find disparate islands or components of nodes within a given network.
A node can reach all the other nodes in the same component when you disregard the relationship direction.

Use the following Cypher query to execute a Weakly-Connected Components algorithm on the Twitch user network.
The _stats_ method of the algorithm is used when we are interested in only high-level statistics of algorithm results.

[source,cypher]
----
CALL gds.wcc.stats('twitch')
YIELD componentCount, componentDistribution
----

With the in-memory graph projection, you can also filter nodes or relationships at algorithm execution time.
In the next example, the WCC algorithm will consider only Stream nodes and connections between them.

[source,cypher]
----
CALL gds.wcc.stats('twitch', {nodeLabels:['Stream']})
YIELD componentCount, componentDistribution
----

== PageRank

PageRank is probably one of the most famous graph algorithms.
It is used to calculate node importance by considering the inbound relationships of a node as well as the importance of the nodes linking to it. PageRank was initially used to calculate the importance of websites by Google, but it can be used in many different scenarios.
Use the following Cypher query to execute the PageRank algorithm on the whole user network.

[source,cypher]
----
CALL gds.pageRank.stream('twitch')
YIELD nodeId, score
WITH nodeId, score
ORDER BY score
DESC LIMIT 10
RETURN gds.util.asNode(nodeId).name as user, score
----

Similarly as with the WCC algorithm, you can choose to run PageRank on the streamer subgraph.

[source,cypher]
----
CALL gds.pageRank.stream('twitch', {nodeLabels:['Stream']})
YIELD nodeId, score
WITH nodeId, score
ORDER BY score
DESC LIMIT 10
WITH gds.util.asNode(nodeId) as node,score
RETURN node.name as streamer,
       score,
       size((node)<--(:Stream)) as relationships_from_streamers,
       size((node)<--(:User)) as relationships_from_users
----

== Community detection

Community detection or clustering algorithms are used to infer the community structure of a given network.
Communities are vaguely defined as groups of nodes within a network that are more densely connected to one another than to other nodes.
With the following Cypher query, you can find the ten largest communities.

[source,cypher]
----
CALL gds.louvain.stream('twitch', {nodeLabels:['Stream']})
YIELD nodeId, communityId
RETURN communityId, count(*) as communitySize
ORDER BY communitySize DESC LIMIT 10
----

== Shared audience analysis

First, drop the current in-memory projected graph.

[source,cypher]
----
CALL gds.graph.drop("twitch")
----

You will examine the shared audience between streamers who play Chess or Poker on stream.
You can pick any other games if you so wish.
To simplify queries, you will first tag the mentioned streamers with a secondary label.

[source,cypher]
----
MATCH (s:Stream)-[:PLAYS]->(g:Game)
WHERE g.name in ["Chess", "Poker"]
SET s:PokerChess
----

Next, you will use `apoc.periodic.iterate` procedure to batch update users who have an out-degree higher than 1.
With this step, you will filter regular users who have chatted in more than a single stream.

[source,cypher]
----
CALL apoc.periodic.iterate("
    MATCH (u:User)
    WHERE NOT u:Stream AND size((u)-->(:Stream)) > 1 
    RETURN u",
    "SET u:Audience",
    {batchSize:50000, parallel:true}
)
----

== Node Similarity algorithm

You need to infer a new network that depicts which streamers share their audience before we can run the Node Similarity algorithm.

[source,cypher]
----
CALL gds.graph.create('shared-audience', 
  ['PokerChess', 'Audience'],
  {CHATTERS: {type:'*', orientation:'REVERSE'}})
----

The Node Similarity algorithm uses the Jaccard similarity coefficient to compare how similar a pair of nodes are.
We will assume that if two streamers share at least 5% of the audience, we will create a relationship between them.
The _mutate_ mode of the algorithms stores the results back to the in-memory projected graph.
This way, you can use the results of one algorithm as an input to another graph algorithm.

[source,cypher]
----
CALL gds.nodeSimilarity.mutate('shared-audience',
 {similarityCutoff:0.05, topK:15, mutateProperty:'score', mutateRelationshipType:'SHARED_AUDIENCE'})
----

== Community detection of shared audience network

With the inferred shared audience network, you can go ahead and run the community detection Louvain algorithm on it.

[source,cypher]
----
CALL gds.louvain.stream('shared-audience', 
       { nodeLabels:['PokerChess'],
         relationshipTypes:['SHARED_AUDIENCE'], 
         relationshipWeightProperty:'score'})
YIELD nodeId, communityId
RETURN communityId, count(*) as communitySize, collect(gds.util.asNode(nodeId).name) as members
ORDER BY communitySize DESC 
LIMIT 5
----

== Next steps

You can try to perform the shared audience analysis on streamers who play other games.
If you want to, you can dig deeper into the moderator or the VIP network by considering only those relatioships.
You can also test out other centrality or community detection algorithms and see how their score compares to PageRank.
